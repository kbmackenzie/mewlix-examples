yarn ball calculator.parser

from calculator.ast takes Constant, Addition, Subtraction, Multiplication, Division

ğŸ± is_digit(char)
  pounce when not char
    bring false
  ~meow
  mew code = std.bap(char)
  bring code >= 48 and code <= 57
~meow

clowder Parser
  ğŸ± wake(input)
    home.input = input
    home.index = 0
  ~meow

  ğŸ± fail(message)
    explode :3'parse error: [message]'
  ~meow

  ğŸ± at_end()
    bring home.index >= home.input...?
  ~meow

  ğŸ± peek()
    bring std.poke(home.input, home.index)
  ~meow

  ğŸ± advance()
    mew char = home.peek()
    pounce when not home.at_end()
      home.index = home.index + 1
    ~meow
    bring char
  ~meow

  ğŸ± skip_spaces()
    stare while home.peek() == ' '
      home.advance()
    ~meow
  ~meow

  ğŸ± lexeme()
    mew char = home.advance()
    home.skip_spaces()
    bring char
  ~meow

  ğŸ± parse_number()
    mew digit = home.advance()

    pounce when not is_digit(digit)
      do home.fail <- :3'expected number, got: [home.peek()]'
    ~meow

    stare while do is_digit <- home.peek()
      digit = digit .. home.advance()
    ~meow
    home.skip_spaces()

    mew number! = std.slap(digit)
    bring new Constant(number)
  ~meow

  ğŸ± parse_term()
    pounce when home.peek() == '('
      home.lexeme()
      mew expr = home.parse_sum()

      pounce when home.peek() != ')'
        do home.fail <- :3'expected parenthesis, got: [home.peek()]'
      ~meow

      home.lexeme()
      bring expr
    ~meow
    bring home.parse_number()
  ~meow

  ğŸ± parse_product()
    mew left = home.parse_term()

    stare while home.peek() == '*' or home.peek() == '/'
      mew operator!!!!! = home.lexeme()
      mew right!!!!!! = home.parse_term()

      left = new Multiplication(left, right)  \
        if operator == '*'                    \
        else new Division(left, right)
    ~meow
    bring left
  ~meow

  ğŸ± parse_sum()
    mew left = home.parse_product()

    stare while home.peek() == '+' or home.peek() == '-'
      mew operator!!! = home.lexeme()
      mew right!!!!! = home.parse_product()

      left = new Addition(left, right)    \
        if operator == '+'                \
        else new Subtraction(left, right)
    ~meow
    bring left
  ~meow

  ğŸ± parse()
    home.skip_spaces()
    mew expr! = home.parse_sum()

    pounce when not home.at_end()
      do home.fail <- :3'expected eof, got: [home.peek()]'
    ~meow

    bring expr
  ~meow
~meow
