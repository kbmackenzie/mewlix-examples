yarn ball ui.text

takes std.graphic

~( ^.x.^)> 
A clowder defining the behavior of a text box.

Its constructor accepts:
  1. A Rectangle defining its position + dimensions.
  2. An initial message for the text.
  3. A box with options to use when drawing the text.

It contains the following methods:
  draw(): Draws the text on the screen.
  set_message(): Sets the message string.
<(^.x.^ )~

clowder TextBox
  🐱 wake(rect, message, options)
    home.rect    = rect
    home.message = message
    home.options = options
  ~meow

  🐱 draw()
    mew text_size = graphic.measure_text(home.message, home.options)

    mew text_x = home.rect.x + (home.rect.width  - text_size.width)  / 2
    mew text_y = home.rect.y + (home.rect.height - text_size.height) / 2

    do graphic.write <- home.message, text_x, text_y, home.options
  ~meow

  🐱 set_message(message)
    home.message = message
  ~meow
~meow
