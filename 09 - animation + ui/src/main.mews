yarn ball main

takes std.graphic
takes mewly
takes palette
takes utils.canvas
takes utils.position
from controls takes Controls
from animation.player takes AnimationPlayer
from utils.fps takes FpsCounter

~( ^.x.^)>
The main yarn ball. A lot of the code here is self-documenting.
See the comments on each yarn ball imported here.
<(^.x.^ )~

------------------------------------------------------------------------
-- Animation:
------------------------------------------------------------------------
mew anim! = new AnimationPlayer(mewly.frames, mewly.frame_rate)

🐱 draw_anim()
  mew pos = do position.center <- mewly.width, mewly.height
  do anim.draw <- pos.x, pos.y
~meow

🐱 set_anim_speed(percentage)
  mew new_frame_rate = do std.floor <- mewly.frame_rate * (percentage / 100)
  do anim.set_frame_rate <- new_frame_rate
~meow

------------------------------------------------------------------------
-- Title:
------------------------------------------------------------------------
mew title_string! = "set animation speed"
mew title_options! = 📦 [
  size: 12,
  color: palette.crepe,
]

🐱 draw_title()
  mew text_size = do graphic.measure_text <- title_string, title_options

  mew x = (canvas.width - text_size.width) / 2
  mew y = do position.row <- 1
  do graphic.write <- title_string, x, y, title_options
~meow

------------------------------------------------------------------------
-- Controls:
------------------------------------------------------------------------
mew controls! = new Controls <- 6

🐱 draw_controls()
  controls.draw()
  controls.update()
  do set_anim_speed <- controls.get_speed()
~meow

------------------------------------------------------------------------
-- Game loop:
------------------------------------------------------------------------
mew fps! = new FpsCounter()
mew fps_options! = 📦 [ color: palette.cream, ]

🐱 game_loop()
  do graphic.paint <- palette.lilac
  draw_anim()
  draw_title()
  draw_controls()

  fps.tick()
  fps.draw(1, 0, fps_options)
~meow

do graphic.init_ <- game_loop
