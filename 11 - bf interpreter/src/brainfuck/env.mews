yarn ball brainfuck.env

clowder Input
  🐱 wake(input)
    home.input = input
    home.index = 0
  ~meow

  🐱 get()
    mew head!! = std.poke(home.input, home.index)
    home.index = home.index + 1
    bring head
  ~meow

  🐱 move(amount)
    home.index = home.index + amount
  ~meow
~meow

clowder Environment
  🐱 wake(stdin)
    home.cells    = std.table()
    home.pointer  = 0
    home.output   = ""
    home.input = new Input(stdin)
  ~meow

  🐱 peek()
    bring home.cells.get(home.pointer) or 0
  ~meow

  🐱 move(offset)
    home.pointer = home.pointer + offset
  ~meow

  🐱 add(value)
    home.cells.add(home.pointer, (home.peek() + value) % 256)
  ~meow

  🐱 set(value)
    home.cells.add(home.pointer, value % 256)
  ~meow

  🐱 puts(count)
    stare while count > 0
      home.output = home.output .. std.char(home.peek())
      count = count - 1
    ~meow
  ~meow

  🐱 gets(count)
    pounce when count > 1
      home.input.move(count - 1)
    ~meow
    mew char! = home.input.get()
    pounce when char != nothing
      home.set(std.bap(char))
    ~meow
  ~meow

  🐱 get_output()
    bring home.output
  ~meow
~meow
