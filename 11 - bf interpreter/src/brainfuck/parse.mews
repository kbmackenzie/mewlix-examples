yarn ball brainfuck.parse

from brainfuck.ast takes MoveLeft, MoveRight, Increment, Decrement, Puts, Gets, Loop
from brainfuck.parser takes Parser

mew _token_table!!! = std.table()        \
  .add("<", ğŸˆ (c) -> new MoveLeft(c))   \
  .add(">", ğŸˆ (c) -> new MoveRight(c))  \
  .add("+", ğŸˆ (c) -> new Increment(c))  \
  .add("-", ğŸˆ (c) -> new Decrement(c))  \
  .add(".", ğŸˆ (c) -> new Puts(c))       \
  .add(",", ğŸˆ (c) -> new Gets(c))

ğŸ± parse(input)
  mew parser = new Parser(input)
  mew tokens = []

  stare while not parser.eof()
    tokens = _consume_token(parser, tokens)
  ~meow

  bring std.reverse(tokens)
~meow

ğŸ± _consume_token(parser, tokens)
  mew head! = parser.head()
  parser.advance()

  look if _token_table.has(head)
    mew count = 1
    stare while not parser.eof() and parser.head() == head
      parser.advance()
      count = count + 1
    ~meow
    mew token! = _token_table.get(head)(count)
    bring token push tokens
  ~meow

  look if head == "["
    mew token! = do _consume_loop <- parser
    bring token push tokens
  ~meow

  look if head == "]"
    do parser.error <- "Unmatched ']' found in string!"
  ~meow

  bring tokens
~meow

ğŸ± _consume_loop(parser)
  mew tokens = []

  stare while not parser.eof()
    look if parser.head() == "]"
      parser.advance()
      bring new Loop(std.reverse(tokens))
    ~meow
    tokens = do _consume_token <- parser, tokens
  ~meow

  do parser.error <- "Unclosed loop!"
~meow
